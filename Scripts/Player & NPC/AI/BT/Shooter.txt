tree("Root")
	parallel
		repeat
			tree("Detections")
		repeat
			fallback
				while IsStatus(AIStatus.Hostile)
					tree("Hostile")
				while IsStatus(AIStatus.Suspicious)
					tree("Search")
				while IsStatus(AIStatus.Calm)
					tree("Patrol")

tree("Detections")
	fallback
		while CanSeeEnemy
			parallel
				LookAtEnemy
				sequence
					EnableHeadLookController(true)
					EnableAimAtController(true)
					IncreaseSeeEnemyTimer
					TimerSetStats
		while not CanSeeEnemy
			sequence
				EnableHeadLookController(false)
				EnableAimAtController(false)
				DecreaseSeeEnemyTimer
				TimerSetStats
		
		
tree("Hostile")
	fallback
		while IsStatus(AIStatus.Hostile)
			fallback
				while not CanSeeEnemy
					fallback
						while IsCombatType(CombatType.Shooter)
							LookAtLastEnemyLocation
						while IsCombatType(CombatType.Melee)
							MoveToLastEnemyLocation
				while InMeleeRange
					parallel
						LookAtEnemy
						repeat
							random
								sequence
									PlayRandomMeleeAttack
									WaitRandom(0.5,1.0)
								Wait(0.2)	
				while not InMeleeRange
					fallback
						while IsCombatType(CombatType.Shooter)
							parallel
								LookAtEnemy
								sequence
									MoveToNearestCover
									WaitArrival
									while InValidCover
										repeat
											random
												MakeRangedAttack
												WaitRandom(0.5,3.0)
												SetCover(true,true)
									Succeed
						while IsCombatType(CombatType.Melee)
							parallel
								LookAtEnemy
								sequence
									MoveToEnemy
									Succeed
		while not IsStatus(AIStatus.Hostile)
			Succeed

tree("Search")
	fallback
		while IsStatus(AIStatus.Suspicious)
			fallback
				while CanSeeEnemy
					StopMoving
				while not CanSeeEnemy
					fallback
						while HasLastEnemyLocation
							sequence
								SetCover(false,false)
								MoveToLastEnemyLocation
								WaitArrival
								RemoveLastEnemyLocation
								WaitRandom(0.5,1.0)
								Succeed
						while not HasLastEnemyLocation
							sequence
								SetCover(false,false)
								repeat
									sequence
										MoveToRandomPoint
										WaitArrival
										WaitRandom(0.5,3.0)
										Succeed
								SetStatus(AIStatus.Calm)
		Succeed

tree("Patrol")
	fallback
		while IsStatus(AIStatus.Calm)
			repeat
				sequence
					SetCover(false,false)
					MoveToWaypoint
					WaitArrival
					WaitRandom(0.5,3.0)
					Succeed
		Succeed
